#!/usr/bin/perl

# run a command every X seconds when a file/dir changes
# -samy kamkar
#
# eg: onchange [-s(ize)] project/ rsync -av project/ remote.com:proj

use strict;
my ($secs, $size, $files, $commands) = usage();

my $stat = $size ? 7 : 9; # size vs mtime
my @files = @{$files};
my @commands = @{$commands};

my (%last, %mtime);
while (1)
{
	my $change;
	foreach my $file (@files)
	{
		($mtime{$file}) = (stat($file))[$stat];
		if ($last{$file} != $mtime{$file})
		{
			$change++;
			$last{$file} = $mtime{$file};
		}
	}

	if ($change)
	{
		print "\$ @commands\n";
		system(@commands);
	}
	select(undef, undef, undef, $secs);
}

sub usage
{
	$|++;

	my $size;
	if ($ARGV[0] eq "-s")
	{
		$size = shift(@ARGV);
	}

	my $secs = 0.1;
	if ($ARGV[0] =~ /^(\d+(\.\d*)?|\.\d+)$/)
	{
		$secs = shift(@ARGV);
	}

	my @files;
	# find our files to monitor, though if we pass a ./executable then consider that the first command to run
	push(@files, find(shift(@ARGV))) while -e $ARGV[0] && !($ARGV[0] =~ m~^\./~ && -x $ARGV[0]);

	# allow -- to split between files vs exec manually
	shift(@ARGV) if $ARGV[0] eq "--";
	my @command = @ARGV;

	# if there's no command and our first file is executable, then run that
	if (!@command && -x $files[0])
	{
		@command = $files[0] =~ m~^[./]~ ? $files[0] : "./$files[0]";
	}

	if (!@files || !@command)
	{
		#print STDERR "secs=$secs\nfiles=@files\ncommand=@command\n";
		die "usage: $0 [secs] [files/dirs] [--] [command to run on change]\n";
	}

	return ($secs, $size, \@files, \@command);
}

sub find
{
	my @files = @_;
	foreach my $path (@_)
	{
		if (-d $path)
		{
			opendir(DIR, $path);
			map { push @files, find("$path/$_") } grep { !/^\.\.?$/ } readdir(DIR);
			closedir(DIR);
		}
	}
	return @files;
}
